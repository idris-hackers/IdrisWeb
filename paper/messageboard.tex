\section{Extended Example: Message Board}
\label{messageboard}
In this section we consider a larger example---a message board application
which allows users to register, log in, view and create threads, and list and
create new posts in threads. 

Firstly, we create a database schema in which to record information stored by
the message board. We create three tables: \texttt{Users}, which contains a
unique User ID, usernames and passwords; \texttt{Threads}, which contains a
unique thread ID, a title, and the ID of the user who created the thread; and
\texttt{Posts}, which contains a unique post ID, the ID of the thread to which
each post belongs, the content of the post, and the ID of the user that created
the post.

Secondly, we use a GET variable, \texttt{action}, to indicate which page of the
message board should be displayed, and pattern-match on these to call the
appropriate function which displays the page. Some pages, such as the page
which shows all of the posts in a thread, require a second argument,
\texttt{thread\_id}.  \subsection{Handling requests}
The entry point to any CGI application is the \texttt{main} function. From here, we run the remainder of the program through a call to \texttt{runCGI}, which we initialise with empty initial environments for the CGI, Session and SQLite effects, so they may be used in further computations. 

\begin{SaveVerbatim}{msgmain}

main : IO ()
main = do (runCGI [initCGIState, 
                  InvalidSession, ()] 
           handleRequest)
          return ()

\end{SaveVerbatim}
\useverb{msgmain}

\noindent
We specify a function, \texttt{handleRequest}, which firstly calls \texttt{isHandlerSet} to determine whether submitted form data must be handled. If so, then the form handling routine is called, which executes the corresponding handler function as specified in Section ~\ref{formhandling}. If not, then the \texttt{handleNonFormRequest} function is called, which inspects the GET variables in order to display the correct page.

\begin{SaveVerbatim}{handlereq}

handleRequest : CGIProg 
     [SESSION (SessionRes SessionUninitialised), 
      SQLITE ()] ()
handleRequest = do 
  handler_set <- isHandlerSet
  if handler_set then do
    lift' (handleForm handlers)
    Effects.return ()
  else do
    action <- lift' (queryGetVar "action")
    thread_id <- lift' (queryGetVar "thread_id")
    handleNonFormRequest action (map strToInt thread_id)

\end{SaveVerbatim}
\useverb{handlereq}

\subsection{Thread Creation}
We specify four forms: one to handle registration, one to handle logging in,
one to handle the creation of new threads, and one to handle the creation of
new posts. For example, the form used to create a new thread
contains elements for the title of the new thread and the content of the
first post of the new thread:

\begin{SaveVerbatim}{newthread}

newThreadForm : UserForm
newThreadForm = do
  addTextBox "Title" FormString Nothing
  addTextBox "Post Content" FormString Nothing 
  useEffects [CgiEffect, SessionEffect, SqliteEffect]
  addSubmit handleNewThread handlers

\end{SaveVerbatim}
\useverb{newthread}

\noindent
This consists of two text boxes: one for the title of the thread, and one
for the content of the first post. Both are of type \texttt{String}, as denoted
by the \texttt{FormString} argument, and both have no default value. The
handler function may make use of the \texttt{CGI}, \texttt{SESSION} and
\texttt{SQLITE} effects, and the handler function is specified as
\texttt{handleNewThread}. The \texttt{handlers} argument refers to the list of
form handlers, and is of the following form:

\begin{SaveVerbatim}{msghandlers}

handlers : HandlerList
handlers = [
 (handler args=[FormString, FormString], 
          effects=[CgiEffect, SessionEffect, SqliteEffect], 
          fn=handleRegisterForm, 
          name="handleRegisterForm"),
  
 (handler args=[FormString, FormString], 
          effects=[CgiEffect, SessionEffect, SqliteEffect], 
          fn=handleNewThread, 
          name="handleNewThread"),
     ...]

\end{SaveVerbatim}
\noindent
\useverb{msghandlers}

\noindent
Creating a new thread (show in Figure \ref{fig:handlethread})
requires a user to be logged in, so that the thread
starter may be recorded in the database. In order to do this, we make use of
the session handler. We define a function \texttt{withSession}, which attempts
to retrieve the session associated with the current request, and if it exists,
executes a function which is passed the associated session data. If not, then a
failure function is called instead. Should the form handler function be called
with invalid arguments, an error is shown.

\begin{SaveVerbatim}{handlenewthread}
handleNewThread : 
  Maybe String -> Maybe String -> 
  FormHandler [CGI (InitialisedCGI TaskRunning), 
               SESSION (SessionRes SessionUninitialised), 
               SQLITE ()]  
handleNewThread (Just title) (Just content) = do 
  withSession (addNewThread title content) notLoggedIn
  return ()
handleNewThread _ _ = do 
  outputWithPreamble "<h1>Error</h1><br />There was 
       an error posting your thread."
  return ()
\end{SaveVerbatim}

\begin{figure}[h]
\useverb{handlenewthread}
\caption{Thread Creation}
\label{fig:handlethread}
\end{figure}

\noindent
Once we have loaded the session data from the database, we then check whether
the \texttt{UserID} variable is set, which demonstrates that a user has
successfully logged into the system, and allows us to use the ID in subsequent
computations. The database operation to insert the thread into the database is
performed by \texttt{threadInsert}, shown in Figure \ref{fig:threadins}.
This uses a library function \texttt{executeInsert}, which abstracts over the
low-level resource usage protocol, enabling for provably-correct database
access without the excess boilerplate code. In addition, \texttt{executeInsert}
returns the unique row ID of the last item which was inserted, which may be
used in subsequent computations. In the case of the message board, we use this
to associate the first post of the thread with the thread being inserted.

\begin{SaveVerbatim}{threadins}
threadInsert : Int -> String -> String -> 
               Eff IO [SQLITE ()] Bool
threadInsert uid title content = do
  let query = "INSERT INTO `Threads` 
    (`UserID`, `Title`) VALUES (?, ?)"
  insert_res <- (executeInsert DB_NAME query 
    [(1, DBInt uid), (2, DBText title)]
  case insert_res of
    Left err => return False
    Right thread_id => postInsert uid thread_id content
\end{SaveVerbatim}

\begin{figure}[h]
\useverb{threadins}
\caption{Thread Insertion}
\label{fig:threadins}
\end{figure}

\subsection{Listing Threads}

Listing the threads in the database is achieved using 
\texttt{executeSelect}, which returns either a
\texttt{ResultSet} or an error:

\begin{SaveVerbatim}{getthreads}

getThreads : Eff IO [SQLITE ()] (Either String ResultSet)
getThreads = 
    executeSelect DB_NAME query [] collectThreadResults
 where query = "SELECT `ThreadID`, `Title`, `UserID`, 
         `Username` FROM `Threads` NATURAL JOIN `Users`"

\end{SaveVerbatim}
\noindent
\useverb{getthreads}

\noindent 
Once the result set has been retrieved, we may iterate through the
results and output them to the page, including a link to a page which shows the
posts associated with the thread. This is shown in Figure \ref{fig:traverse}.
Since we know the structure of the returned
row from designing the query, we may pattern match on each returned row to make
use of the returned values.

\begin{SaveVerbatim}{traversethreads}
traverseThreads : ResultSet -> 
  Eff IO [CGI (InitialisedCGI TaskRunning)] ()
traverseThreads [] = return ()
traverseThreads (x::xs) = do traverseRow x
                             traverseThreads xs
  where traverseRow : List DBVal -> 
           Eff IO [CGI (InitialisedCGI TaskRunning)] ()
        traverseRow ((DBInt thread_id)::
                     (DBText title)::
                     (DBInt user_id)::
                     (DBText username)::[]) =
           (output $ "<tr><td>
            <a href=\"?action=showthread&thread_id=" ++ 
            (show thread_id) ++ "\">" ++ 
            title ++ "</a></td><td>" ++ 
            username ++ "</td></tr>") 
        traverseRow _ = return ()
\end{SaveVerbatim}

\begin{figure}[h]
\useverb{traversethreads}
\caption{Thread Insertion}
\label{fig:traverse}
\end{figure}

\subsection{Authentication}

Once a user submits the login form, the associated handler queries the database
to ascertain whether a user with the given username and password exists through
a call to the \texttt{authUser} function. This is shown in Figure
\ref{fig:handlelogin}. If so, then the session handler is
invoked, and a session is initialised with the user ID retrieved from the
database. The session ID is then set as a cookie using the CGI effect, so that
it may be used in subsequent requests. Any failures, for example with creating
a new session or querying the database, are reported to the user.

\begin{SaveVerbatim}{handlelogin}
handleLoginForm (Just name) (Just pwd) = do
  auth_res <- lift' (authUser name pwd)
  case auth_res of
    Right (Just uid) => do
      set_sess_res <- setSession uid
      if set_sess_res then do
        lift' (output $ "Welcome, " ++ name)
        return ()
      else do
        lift' (output "Could not set session")
         return ()
    Right Nothing => do
      lift' (output "Invalid username or password")
      return ()
    Left err => do
      lift' (output $ "Error: " ++ err)
      return ()
\end{SaveVerbatim}

\begin{figure}[h]
\useverb{handlelogin}
\caption{Thread Insertion}
\label{fig:handlelogin}
\end{figure}

Implementations for the insertion and display of posts, alongside registration,
are similar in form to those described above, and are as such omitted from this
paper.

Although we have described a relatively simple application, we have shown that
through the use of embedded domain-specific languages, we may write verified
code that fails to compile should resources be incorrectly accessed.
Additionally, we have used the form handling mechanism to simply handle the
arguments passed by the user. Importantly, we have shown that dependent types
can be used to increase confidence in an (albeit simplified) real-world
application, without requiring developers to supply proofs or indeed work
explicitly with dependent types. 

%-----------------------------
%-----------------------------